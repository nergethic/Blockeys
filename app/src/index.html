<!DOCTYPE html> 
<html lang="en"> 
  <head> 
    <meta charset="UTF-8"> 
    <meta name="viewport" content="width=device-width, initial-scale=1.0"> 
    <meta http-equiv="X-UA-Compatible" content="ie=edge"> 
    <title>Blockeys</title> 
  </head> 
  <body> 
    <div id="root" class="ad-App"></div>
  </script>

  <script id="simple-vertex" type="x-shader/x-vertex">
    attribute vec3 a_VertexPos;

    void main() {
      gl_Position = vec4(a_VertexPos, 1.0);
    }
  </script>

  <script id="simple-fragment" type="x-shader/x-fragment">
    precision mediump float;

    //uniform float iTime;
    //uniform vec2 iResolution;

    //varying vec4 colorOut;

    void main()
    {
        gl_FragColor = vec4(0.3, 0.2, 0.6, 1.0);
    }
  </script>

  <script id="vertex-color" type="x-shader/x-vertex">
    attribute vec3 a_VertexPos;
    attribute vec4 a_Color;

    uniform mat4 iModel;
    uniform mat4 iView;
    uniform mat4 iProjection;

    varying vec4 colorOut;

    void main() {
      colorOut = a_Color;
      vec4 newPosition = iProjection * iView * iModel * vec4((a_VertexPos), 1.0);
      gl_Position = newPosition;
    }
  </script>

  <script id="fragment-color" type="x-shader/x-fragment">
    precision mediump float;

    uniform float iTime;

    varying vec4 colorOut;

    void main()
    {
        vec4 outputColor = colorOut + sin(iTime);
        gl_FragColor = outputColor;
    }
  </script>

  <script id="vertex-normal" type="x-shader/x-vertex">
    attribute vec3 a_VertexPos;
    attribute vec4 a_Color;
    attribute vec3 a_Normal;

    uniform mat4 iModel;
    uniform mat4 iView;
    uniform mat4 iProjection;

    varying vec4 colorOut;
    varying vec3 normalOut;
    varying vec3 fragPosOut;

    void main() {
      colorOut = a_Color;
      normalOut = a_Normal;
      fragPosOut = vec3(iModel * vec4((a_VertexPos), 1.0));

      vec4 newPosition = iProjection * iView * vec4(fragPosOut, 1.0);
      gl_Position = newPosition;
    }
  </script>

  <script id="fragment-normal" type="x-shader/x-fragment">
    precision mediump float;

    uniform vec3 iTintColor;
    uniform float iTime;
    uniform vec3 iLightPosition; 
    uniform vec3 iViewPosition;
    //uniform vec3 lightColor;

    varying vec4 colorOut;
    varying vec3 normalOut;
    varying vec3 fragPosOut;

    void main()
    {
        float ambientStrength = 0.1;
        vec3 ambient = ambientStrength * vec3(1.0, 1.0, 1.0); //lightColor;
        
        // diffuse 
        vec3 norm = normalize(normalOut);
        vec3 lightDir = normalize(iLightPosition - fragPosOut);
        float diff = max(dot(norm, lightDir), 0.0);
        vec3 diffuse = diff * vec3(1.0, 1.0, 1.0); //lightColor;

        vec3 viewDir = normalize(iViewPosition - fragPosOut);
        vec3 reflectDir = reflect(-lightDir, norm);
        float specularStrength = 0.9;
        float spec = pow(max(dot(viewDir, reflectDir), 0.0), 32.0);
        vec3 specular = specularStrength * spec * vec3(1.0, 1.0, 1.0); // lightColor; 
                
        vec3 result = (ambient + diffuse + specular) * iTintColor; // + sin(iTime)*0.1;
        gl_FragColor = vec4(result, colorOut.a);
    }
  </script>

  <script id="fragment-fbm" type="x-shader/x-fragment">
    precision mediump float;

    uniform float iTime;
    uniform vec2 iResolution;
    uniform vec4 iTintColor;

    varying vec4 colorOut;

    #define NUM_OCTAVES 5

  float rand(vec2 n) { 
    return fract(sin(dot(n, vec2(12.9898, 4.1414))) * 43758.5453);
  }

  float noise(vec2 p){
    vec2 ip = floor(p);
    vec2 u = fract(p);
    u = u*u*(3.0-2.0*u);
    
    float res = mix(
      mix(rand(ip),rand(ip+vec2(1.0,0.0)),u.x),
      mix(rand(ip+vec2(0.0,1.0)),rand(ip+vec2(1.0,1.0)),u.x),u.y);
    return res*res;
  }

  float fbm(vec2 x) {
    float v = 0.0;
    float a = 0.5;
    vec2 shift = vec2(100);
    // Rotate to reduce axial bias
      mat2 rot = mat2(cos(0.5), sin(0.5), -sin(0.5), cos(0.50));
    for (int i = 0; i < NUM_OCTAVES; ++i) {
      v += a * noise(x);
      x = rot * x * 2.0 + shift;
      a *= 0.5;
    }
    return v;
  }

  // https://www.iquilezles.org/www/articles/warp/warp.htm
  float pattern( in vec2 p )
  {
      vec2 q = vec2( fbm( p + vec2(0.0,0.0) ),
                    fbm( p + vec2(5.2,1.3) ) );

      return fbm( p + 4.0*q );
  }

  vec3 colour(float x) {
      vec3 a = vec3(0.5, 0.5, 0.5);
      vec3 b = vec3(0.5, 0.5, 0.5);
      vec3 c = vec3(1.0, 1.0, 1.0);
      vec3 d = vec3(0.0, 0.0, 0.0);
      
      return a + b * cos(6.28318*(c*x+d));
  }

  vec2 rotate(vec2 v, float a) {
    float s = sin(a);
    float c = cos(a);
    mat2 m = mat2(c, -s, s, c);
    return m * v;
  }

  void main()
  {
      vec2 uv = gl_FragCoord.xy / iResolution.xy;
      uv = 0.7*abs(2.0*(uv-0.5));
      uv.y += uv.x / 10.0;
      float noise = fbm(iTime*0.2+uv*10.0) / 4.0;
      float noiseTex = pattern(rotate(uv*10.0*(1.0+(0.05*sin(iTime*0.5))), iTime*0.01)) * 0.5+cos(0.2*iTime+sin(iTime)*0.2);
      
      float inside = (sin((uv.x+uv.y))*10.0) + (uv.y*10.0+uv.x*10.0);
      float mask = (uv.y*2.0 + sin(inside)) / 1.5;
      
      vec3 col = colour(noiseTex+mask);
      col.rg += -abs(rotate(uv, 0.3*iTime+noise*10.0).xy);

      vec4 outputColor = colorOut + iTintColor;
      outputColor.xyz += col;
      gl_FragColor = outputColor;
  }
  </script>
  </body>
</html>